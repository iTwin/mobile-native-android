//---------------------------------------------------------------------------------------
//
// Copyright (c) Bentley Systems, Incorporated. All rights reserved.
// See LICENSE.md in the project root for license terms and full copyright notice.
//
//---------------------------------------------------------------------------------------
apply plugin: 'maven-publish'

// Downloads the AAR file from releases using wget so it can then be published to the local Maven repo
task assemble(type: Exec) {
    commandLine 'wget', '-qN', 'https://github.com/iTwin/mobile-native-android/releases/download/5.7.3/iTwinAndroidLibrary.aar'
}

// An alternative task for downloading (useful for authenticated downloads)
task assembleGitHub(type: Exec) {
    commandLine 'gh', 'release', 'download', '5.7.3', '-p', 'iTwinAndroidLibrary.aar'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/itwin/mobile-native-android")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME") ?: "token"
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.itwin'
            version = '5.7.3'
            artifact('iTwinAndroidLibrary.aar') {
                extension 'aar'
            }
            pom {
                name = 'iTwin.js Mobile Native'
                description = 'iTwin.js Native library'
                url = 'http://github.com/iTwin/mobile-native-android'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/iTwin/mobile-native-android/blob/main/LICENSE.md'
                    }
                }
            }
        }
    }
}
